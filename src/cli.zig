const args = @import("args");
const builtin = @import("builtin");
const std = @import("std");
const kiesel = @import("kiesel");
const Linenoise = @import("linenoise").Linenoise;

const gc = @cImport({
    @cInclude("gc.h");
});

const Allocator = std.mem.Allocator;

const Agent = kiesel.execution.Agent;
const ArgumentsList = kiesel.builtins.ArgumentsList;
const Diagnostics = kiesel.language.Diagnostics;
const Object = kiesel.types.Object;
const Realm = kiesel.execution.Realm;
const Script = kiesel.language.Script;
const Value = kiesel.types.Value;
const defineBuiltinFunction = kiesel.utils.defineBuiltinFunction;
const defineBuiltinProperty = kiesel.utils.defineBuiltinProperty;
const ordinaryObjectCreate = kiesel.builtins.ordinaryObjectCreate;

const bdwgc_version_string = std.fmt.comptimePrint("{}.{}.{}", .{
    gc.GC_VERSION_MAJOR,
    gc.GC_VERSION_MINOR,
    gc.GC_VERSION_MICRO,
});

const version = std.fmt.comptimePrint(
    \\kiesel {[kiesel]s}
    \\bdwgc {[bdwgc]s}
    \\zig {[zig]s}
    \\
, .{
    .kiesel = kiesel.version_string,
    .bdwgc = bdwgc_version_string,
    .zig = builtin.zig_version_string,
});

const help = std.fmt.comptimePrint(
    \\Usage: {[color_bold]s}{[color_yellow]s}kiesel{[color_reset]s} {[color_dim]s}[{[color_reset]s}options{[color_dim]s}]{[color_reset]s} {[color_dim]s}[{[color_reset]s}args{[color_dim]s}]{[color_reset]s}
    \\
    \\Options:
    \\
    \\  -{[color_green]s}c{[color_reset]s}, --{[color_cyan]s}command{[color_reset]s}             Run the given code as a script
    \\  --{[color_cyan]s}disable-gc{[color_reset]s}              Disable garbage collection
    \\  --{[color_cyan]s}print-ast{[color_reset]s}               Print the parsed AST
    \\  --{[color_cyan]s}print-bytecode{[color_reset]s}          Print the generated bytecode
    \\  -{[color_green]s}p{[color_reset]s}, --{[color_cyan]s}print-result{[color_reset]s}        Print the evaluated result
    \\  -{[color_green]s}v{[color_reset]s}, --{[color_cyan]s}version{[color_reset]s}             Print version information and exit
    \\  -{[color_green]s}h{[color_reset]s}, --{[color_cyan]s}help{[color_reset]s}                Print help text and exit
    \\
    \\Arguments:
    \\
    \\  {[color_cyan]s}file{[color_reset]s}                      The JS file to run, a REPL is started if omitted
    \\
, .{
    .color_reset = "\x1b[0m",
    .color_bold = "\x1b[1m",
    .color_dim = "\x1b[2m",
    .color_green = "\x1b[92m",
    .color_yellow = "\x1b[93m",
    .color_cyan = "\x1b[96m",
});

const stdout = std.io.getStdOut().writer();
const stderr = std.io.getStdErr().writer();

pub const Kiesel = struct {
    pub fn create(realm: *Realm) !Object {
        const kiesel_object = try ordinaryObjectCreate(realm.agent, try realm.intrinsics.@"%Object.prototype%"());
        const gc_object = try ordinaryObjectCreate(realm.agent, try realm.intrinsics.@"%Object.prototype%"());
        try defineBuiltinFunction(gc_object, "collect", collect, 0, realm);
        try defineBuiltinFunction(kiesel_object, "createRealm", createRealm, 0, realm);
        try defineBuiltinFunction(kiesel_object, "evalScript", evalScript, 1, realm);
        try defineBuiltinProperty(kiesel_object, "gc", Value.from(gc_object));
        try defineBuiltinFunction(kiesel_object, "print", print, 1, realm);
        return kiesel_object;
    }

    fn collect(_: *Agent, _: Value, _: ArgumentsList) !Value {
        kiesel.gc.collect();
        return .undefined;
    }

    fn createRealm(agent: *Agent, _: Value, _: ArgumentsList) !Value {
        const realm = try Realm.create(agent);
        try realm.setRealmGlobalObject(null, null);
        const global = try realm.setDefaultGlobalBindings();
        return Value.from(global);
    }

    /// Algorithm from https://github.com/tc39/test262/blob/main/INTERPRETING.md
    fn evalScript(agent: *Agent, _: Value, arguments: ArgumentsList) !Value {
        const source_text = try arguments.get(0).toString(agent);

        // 1. Let hostDefined be any host-defined values for the provided sourceText (obtained in
        //    an implementation dependent manner)
        const host_defined = null;

        // 2. Let realm be the current Realm Record.
        const realm = agent.currentRealm();

        var diagnostics = Diagnostics.init(agent.gc_allocator);
        defer diagnostics.deinit();

        // 3. Let s be ParseScript(sourceText, realm, hostDefined).
        const script = Script.parse(source_text, realm, host_defined, .{
            .diagnostics = &diagnostics,
            .file_name = "evalScript",
        }) catch |err| switch (err) {
            error.OutOfMemory => return error.OutOfMemory,
            // 4. If s is a List of errors, then
            error.ParseError => {
                // a. Let error be the first element of s.
                const parse_error = diagnostics.errors.items[0];

                // b. Return Completion{[[Type]]: throw, [[Value]]: error, [[Target]]: empty}.
                return agent.throwException(.syntax_error, parse_error.message);
            },
        };

        // 5. Let status be ScriptEvaluation(s).
        const status = script.evaluate();

        // 6. Return Completion(status).
        return status;
    }

    fn print(agent: *Agent, _: Value, arguments: ArgumentsList) !Value {
        const str = try arguments.get(0).toString(agent);
        stdout.print("{s}\n", .{str}) catch {};
        return .undefined;
    }
};

fn run(
    allocator: Allocator,
    realm: *Realm,
    file_name: []const u8,
    source_text: []const u8,
) !?Value {
    const agent = realm.agent;

    var diagnostics = Diagnostics.init(allocator);
    defer diagnostics.deinit();

    const script = Script.parse(source_text, realm, null, .{
        .diagnostics = &diagnostics,
        .file_name = file_name,
    }) catch |err| switch (err) {
        error.ParseError => {
            try diagnostics.print(stderr);
            return null;
        },
        error.OutOfMemory => return error.OutOfMemory,
    };

    if (agent.options.debug.print_ast) try script.ecmascript_code.print(stdout);

    return script.evaluate() catch |err| switch (err) {
        error.OutOfMemory => {
            try stderr.writeAll("Out of memory\n");
            return null;
        },
        error.ExceptionThrown => {
            try stderr.print("Uncaught exception: {pretty}\n", .{agent.exception.?});
            return null;
        },
    };
}

fn getHistoryPath(allocator: Allocator) ![]const u8 {
    const app_data_dir = try std.fs.getAppDataDir(allocator, "kiesel");
    defer allocator.free(app_data_dir);

    const history_path = try std.fs.path.join(allocator, &[_][]const u8{ app_data_dir, "history" });
    errdefer allocator.free(history_path);

    std.fs.makeDirAbsolute(app_data_dir) catch |err| switch (err) {
        error.PathAlreadyExists => {},
        else => return err,
    };

    const file = try std.fs.createFileAbsolute(history_path, .{ .truncate = false });
    file.close();

    return history_path;
}

fn repl(allocator: Allocator, realm: *Realm) !void {
    var linenoise = Linenoise.init(allocator);
    defer linenoise.deinit();

    const history_path = try getHistoryPath(allocator);
    defer allocator.free(history_path);

    linenoise.history.load(history_path) catch stdout.writeAll("Failed to load history\n") catch {};
    defer linenoise.history.save(history_path) catch stdout.writeAll("Failed to save history\n") catch {};

    while (true) {
        if (linenoise.linenoise("> ") catch |err| switch (err) {
            error.CtrlC => continue,
            else => return err,
        }) |source_text| {
            defer allocator.free(source_text);

            // Directly show another prompt when spamming enter, whitespace is evaluated
            // however (and will print 'undefined').
            if (source_text.len == 0) continue;

            try linenoise.history.add(source_text);

            if (try run(allocator, realm, "repl", source_text)) |result| {
                try stdout.print("{pretty}\n", .{result});
            }
            // Handled exception & printed something, carry on
            else continue;
        }
        // ^D pressed, exit REPL
        else break;
    }
}

pub fn main() !u8 {
    var gpa = std.heap.GeneralPurposeAllocator(.{}){};
    defer _ = gpa.deinit();
    const allocator = gpa.allocator();

    const parsed_args = args.parseForCurrentProcess(struct {
        command: ?[]const u8 = null,
        @"disable-gc": bool = false,
        @"print-ast": bool = false,
        @"print-bytecode": bool = false,
        @"print-result": bool = false,
        version: bool = false,
        help: bool = false,

        pub const shorthands = .{
            .c = "command",
            .p = "print-result",
            .v = "version",
            .h = "help",
        };
    }, allocator, .print) catch return 1;
    defer parsed_args.deinit();

    if (parsed_args.options.version) {
        try stdout.writeAll(version);
        return 0;
    } else if (parsed_args.options.help) {
        try stdout.writeAll(help);
        return 0;
    }

    var agent = try Agent.init(.{
        .debug = .{
            .disable_gc = parsed_args.options.@"disable-gc",
            .print_ast = parsed_args.options.@"print-ast",
            .print_bytecode = parsed_args.options.@"print-bytecode",
        },
    });
    defer agent.deinit();
    try Realm.initializeHostDefinedRealm(&agent, .{});
    const realm = agent.currentRealm();
    try defineBuiltinProperty(realm.global_object, "Kiesel", Value.from(try Kiesel.create(realm)));

    const path = if (parsed_args.positionals.len > 0) parsed_args.positionals[0] else null;
    if (path != null) {
        const file = try std.fs.cwd().openFile(path.?, .{});
        defer file.close();
        const file_name = std.fs.path.basename(path.?);
        const source_text = try file.reader().readAllAlloc(allocator, std.math.maxInt(usize));
        defer allocator.free(source_text);
        if (try run(allocator, realm, file_name, source_text)) |result| {
            if (parsed_args.options.@"print-result")
                try stdout.print("{pretty}\n", .{result});
        } else return 1;
    } else if (parsed_args.options.command) |source_text| {
        if (try run(allocator, realm, "command", source_text)) |result| {
            if (parsed_args.options.@"print-result")
                try stdout.print("{pretty}\n", .{result});
        } else return 1;
    } else {
        try repl(allocator, realm);
    }
    return 0;
}
