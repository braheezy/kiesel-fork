const args = @import("args");
const builtin = @import("builtin");
const std = @import("std");
const kiesel = @import("kiesel");

const gc = @cImport({
    @cInclude("gc.h");
});

const Allocator = std.mem.Allocator;

const Agent = kiesel.execution.Agent;
const Diagnostics = kiesel.language.Diagnostics;
const Realm = kiesel.execution.Realm;
const Script = kiesel.language.Script;
const Value = kiesel.types.Value;

const bdwgc_version_string = std.fmt.comptimePrint("{}.{}.{}", .{
    gc.GC_VERSION_MAJOR,
    gc.GC_VERSION_MINOR,
    gc.GC_VERSION_MICRO,
});

const version = std.fmt.comptimePrint(
    \\kiesel {[kiesel]s}
    \\bdwgc {[bdwgc]s}
    \\zig {[zig]s}
    \\
, .{
    .kiesel = kiesel.version_string,
    .bdwgc = bdwgc_version_string,
    .zig = builtin.zig_version_string,
});

const help =
    \\Usage: kiesel [options] [args]
    \\
    \\Options:
    \\
    \\  -c, --command             Run the given code as a script
    \\  --disable-gc              Disable garbage collection
    \\  --print-ast               Print the parsed AST
    \\  --print-bytecode          Print the generated bytecode
    \\  -v, --version             Print version information and exit
    \\  -h, --help                Print help text and exit
    \\
    \\Arguments:
    \\
    \\  file                      The JS file to run, a REPL is started if omitted
    \\
;

fn run(
    allocator: Allocator,
    realm: *Realm,
    file_name: []const u8,
    source_text: []const u8,
) !?Value {
    const stdout = std.io.getStdOut().writer();
    const stderr = std.io.getStdErr().writer();

    const agent = realm.agent;

    var diagnostics = Diagnostics.init(allocator);
    defer diagnostics.deinit();

    const script = Script.parse(source_text, realm, null, .{
        .diagnostics = &diagnostics,
        .file_name = file_name,
    }) catch |err| switch (err) {
        error.ParseError => {
            try diagnostics.print(stderr);
            return null;
        },
        error.OutOfMemory => return error.OutOfMemory,
    };

    if (agent.options.debug.print_ast)
        try script.ecmascript_code.print(stdout);

    return script.evaluate() catch |err| switch (err) {
        error.IndexOutOfRange => {
            try stderr.print("Bytecode generation failed\n", .{});
            return null;
        },
        error.ExceptionThrown => {
            try stderr.print("Uncaught exception: {pretty}\n", .{agent.exception.?});
            return null;
        },
        else => return err,
    };
}

pub fn main() !u8 {
    var gpa = std.heap.GeneralPurposeAllocator(.{}){};
    defer _ = gpa.deinit();
    const allocator = gpa.allocator();

    const stdin = std.io.getStdIn().reader();
    const stdout = std.io.getStdOut().writer();

    const parsed_args = args.parseForCurrentProcess(struct {
        command: ?[]const u8 = null,
        @"disable-gc": bool = false,
        @"print-ast": bool = false,
        @"print-bytecode": bool = false,
        version: bool = false,
        help: bool = false,

        pub const shorthands = .{
            .c = "command",
            .v = "version",
            .h = "help",
        };
    }, allocator, .print) catch return 1;
    defer parsed_args.deinit();

    if (parsed_args.options.version) {
        try stdout.writeAll(version);
        return 0;
    } else if (parsed_args.options.help) {
        try stdout.writeAll(help);
        return 0;
    }

    var agent = try Agent.init(.{
        .debug = .{
            .disable_gc = parsed_args.options.@"disable-gc",
            .print_ast = parsed_args.options.@"print-ast",
            .print_bytecode = parsed_args.options.@"print-bytecode",
        },
    });
    defer agent.deinit();
    try Realm.initializeHostDefinedRealm(&agent, .{});
    const realm = agent.currentRealm();

    if (parsed_args.positionals.len > 0) {
        const path = parsed_args.positionals[0];
        const file = try std.fs.cwd().openFile(path, .{});
        const file_name = std.fs.path.basename(path);
        const file_size = (try file.stat()).size;
        var source_text = try allocator.alloc(u8, file_size);
        defer allocator.free(source_text);
        _ = try file.readAll(source_text);
        _ = try run(allocator, realm, file_name, source_text) orelse return 1;
    } else if (parsed_args.options.command) |source_text| {
        _ = try run(allocator, realm, "command", source_text) orelse return 1;
    } else {
        while (true) {
            try stdout.print("> ", .{});
            if (try stdin.readUntilDelimiterOrEofAlloc(
                allocator,
                '\n',
                std.math.maxInt(usize),
            )) |source_text| {
                defer allocator.free(source_text);

                // Directly show another prompt when spamming enter, whitespace is evaluated
                // however (and will print 'undefined').
                if (source_text.len == 0)
                    continue;

                if (try run(allocator, realm, "repl", source_text)) |result| {
                    try stdout.print("{pretty}\n", .{result});
                }
                // Handled exception & printed something, carry on
                else continue;
            }
            // ^C pressed, exit REPL
            else break;
        }
    }
    return 0;
}
