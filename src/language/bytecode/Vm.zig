const std = @import("std");

const ast = @import("../ast.zig");
const builtins = @import("../../builtins.zig");
const execution = @import("../../execution.zig");
const instructions_ = @import("instructions.zig");
const types = @import("../../types.zig");
const utils = @import("../../utils.zig");

const Agent = execution.Agent;
const ArgumentsList = builtins.ArgumentsList;
const Completion = types.Completion;
const Executable = @import("Executable.zig");
const Instruction = instructions_.Instruction;
const Object = types.Object;
const PropertyKey = types.PropertyKey;
const Reference = types.Reference;
const Value = types.Value;
const arrayCreate = builtins.arrayCreate;
const isLessThan = types.isLessThan;
const isLooselyEqual = types.isLooselyEqual;
const isStrictlyEqual = types.isStrictlyEqual;
const makeConstructor = builtins.makeConstructor;
const newDeclarativeEnvironment = execution.newDeclarativeEnvironment;
const noexcept = utils.noexcept;
const ordinaryFunctionCreate = builtins.ordinaryFunctionCreate;
const ordinaryObjectCreate = builtins.ordinaryObjectCreate;
const performEval = builtins.performEval;
const setFunctionName = builtins.setFunctionName;

const Self = @This();

agent: *Agent,
ip: usize,
stack: std.ArrayList(Value),
reference_stack: std.ArrayList(?Reference),
exception_jump_target_stack: std.ArrayList(usize),
result: ?Value = null,
exception: ?Value = null,
reference: ?Reference = null,

pub fn init(agent: *Agent) !Self {
    var stack = try std.ArrayList(Value).initCapacity(agent.gc_allocator, 32);
    var reference_stack = std.ArrayList(?Reference).init(agent.gc_allocator);
    var exception_jump_target_stack = std.ArrayList(usize).init(agent.gc_allocator);
    return .{
        .agent = agent,
        .ip = 0,
        .stack = stack,
        .reference_stack = reference_stack,
        .exception_jump_target_stack = exception_jump_target_stack,
    };
}

pub fn deinit(self: Self) void {
    self.stack.deinit();
    self.reference_stack.deinit();
    self.exception_jump_target_stack.deinit();
}

fn fetchInstruction(self: *Self, executable: Executable) ?Instruction {
    const instructions = executable.instructions.items;
    if (self.ip >= instructions.len) return null;
    defer self.ip += 1;
    return instructions[self.ip];
}

fn fetchConstant(self: *Self, executable: Executable) Value {
    const constants = executable.constants.items;
    const index = self.fetchIndex(executable);
    return constants[index];
}

fn fetchIdentifier(self: *Self, executable: Executable) []const u8 {
    const identifiers = executable.identifiers.items;
    const index = self.fetchIndex(executable);
    return identifiers[index];
}

fn fetchFunctionExpression(self: *Self, executable: Executable) ast.FunctionExpression {
    const function_expressions = executable.function_expressions.items;
    const index = self.fetchIndex(executable);
    return function_expressions[index];
}

fn fetchIndex(self: *Self, executable: Executable) Executable.IndexType {
    const b1 = @intFromEnum(self.fetchInstruction(executable).?);
    const b2 = @intFromEnum(self.fetchInstruction(executable).?);
    return std.mem.bytesToValue(Executable.IndexType, &[_]u8{ b1, b2 });
}

/// 13.3.5.1.1 EvaluateNew ( constructExpr, arguments )
/// https://tc39.es/ecma262/#sec-evaluatenew
fn evaluateNew(agent: *Agent, constructor: Value, arguments: []const Value) !Value {
    // 1. Let ref be ? Evaluation of constructExpr.
    // 2. Let constructor be ? GetValue(ref).
    // 3. If arguments is empty, then
    //     a. Let argList be a new empty List.
    // 4. Else,
    //     a. Let argList be ? ArgumentListEvaluation of arguments.
    // NOTE: This is part of the generated bytecode.

    // 5. If IsConstructor(constructor) is false, throw a TypeError exception.
    if (!constructor.isConstructor()) {
        return agent.throwException(
            .type_error,
            try std.fmt.allocPrint(agent.gc_allocator, "{} is not a constructor", .{constructor}),
        );
    }

    // 6. Return ? Construct(constructor, argList).
    return Value.from(try constructor.object.construct(.{
        .arguments_list = ArgumentsList.from(arguments),
    }));
}

/// 13.3.6.2 EvaluateCall ( func, ref, arguments, tailPosition )
/// https://tc39.es/ecma262/#sec-evaluatecall
fn evaluateCallGetThisValue(maybe_reference: ?Reference) Value {
    // 1. If ref is a Reference Record, then
    if (maybe_reference) |reference| {
        // a. If IsPropertyReference(ref) is true, then
        if (reference.isPropertyReference()) {
            // i. Let thisValue be GetThisValue(ref).
            return reference.getThisValue();
        }
        // b. Else,
        else {
            // i. Let refEnv be ref.[[Base]].
            // ii. Assert: refEnv is an Environment Record.
            const reference_environment = reference.base.environment;

            // iii. Let thisValue be refEnv.WithBaseObject().
            return if (reference_environment.withBaseObject()) |object|
                Value.from(object)
            else
                .undefined;
        }
    }
    // 2. Else,
    else {
        // a. Let thisValue be undefined.
        return .undefined;
    }
}

/// 13.3.6.2 EvaluateCall ( func, ref, arguments, tailPosition )
/// https://tc39.es/ecma262/#sec-evaluatecall
fn evaluateCall(
    agent: *Agent,
    function: Value,
    this_value: Value,
    arguments: []const Value,
) !Value {
    // 1-2.
    // NOTE: These are done in the function above, which is called by the prepare_call instruction.

    // 3. Let argList be ? ArgumentListEvaluation of arguments.
    // NOTE: This is part of the generated bytecode.

    // 4. If func is not an Object, throw a TypeError exception.
    if (function != .object) {
        return agent.throwException(
            .type_error,
            try std.fmt.allocPrint(agent.gc_allocator, "{} is not an Object", .{function}),
        );
    }

    // 5. If IsCallable(func) is false, throw a TypeError exception.
    if (!function.isCallable()) {
        return agent.throwException(
            .type_error,
            try std.fmt.allocPrint(agent.gc_allocator, "{} is not callable", .{function}),
        );
    }

    // TODO: 6. If tailPosition is true, perform PrepareForTailCall().

    // 7. Return ? Call(func, thisValue, argList).
    return function.callAssumeCallable(this_value, arguments);
}

/// CallExpression : CoverCallExpressionAndAsyncArrowHead
/// Step 6.a.
fn directEval(agent: *Agent, arguments: []const Value, strict: bool) !Value {
    // i. Let argList be ? ArgumentListEvaluation of arguments.
    // NOTE: This is part of the generated bytecode.

    // ii. If argList has no elements, return undefined.
    if (arguments.len == 0) return .undefined;

    // iii. Let evalArg be the first element of argList.
    const eval_arg = arguments[0];

    // iv. If the source text matched by this CallExpression is strict mode code, let strictCaller
    //     be true. Otherwise let strictCaller be false.
    const strict_caller = strict;

    // v. Return ? PerformEval(evalArg, strictCaller, true).
    return performEval(agent, eval_arg, strict_caller, true);
}

/// 13.10.2 InstanceofOperator ( V, target )
/// https://tc39.es/ecma262/#sec-instanceofoperator
fn instanceofOperator(agent: *Agent, value: Value, target: Value) !bool {
    // 1. If target is not an Object, throw a TypeError exception.
    if (target != .object) {
        return agent.throwException(
            .type_error,
            "Right-hand side of 'in' operator must be an object",
        );
    }

    // 2. Let instOfHandler be ? GetMethod(target, @@hasInstance).
    const maybe_instanceof_handler = try target.getMethod(
        agent,
        PropertyKey.from(agent.well_known_symbols.@"@@hasInstance"),
    );

    // 3. If instOfHandler is not undefined, then
    if (maybe_instanceof_handler) |instanceof_handler| {
        // a. Return ToBoolean(? Call(instOfHandler, target, Â« V Â»)).
        return (try Value.from(instanceof_handler).call(agent, target, .{value})).toBoolean();
    }

    // 4. If IsCallable(target) is false, throw a TypeError exception.
    if (!target.isCallable()) {
        return agent.throwException(
            .type_error,
            try std.fmt.allocPrint(agent.gc_allocator, "{} is not callable", .{target}),
        );
    }

    // 5. Return ? OrdinaryHasInstance(target, V).
    return target.ordinaryHasInstance(value);
}

/// 13.15.3 ApplyStringOrNumericBinaryOperator ( lval, opText, rval )
/// https://tc39.es/ecma262/#sec-applystringornumericbinaryoperator
fn applyStringOrNumericBinaryOperator(agent: *Agent, lval: Value, operator: ast.BinaryExpression.Operator, rval: Value) !Value {
    var final_lval = lval;
    var final_rval = rval;

    // 1. If opText is +, then
    if (operator == .@"+") {
        // a. Let lprim be ? ToPrimitive(lval).
        const lprim = try lval.toPrimitive(agent, null);

        // b. Let rprim be ? ToPrimitive(rval).
        const rprim = try rval.toPrimitive(agent, null);

        // c. If lprim is a String or rprim is a String, then
        if (lprim == .string or rprim == .string) {
            // i. Let lstr be ? ToString(lprim).
            const lstr = try lprim.toString(agent);

            // ii. Let rstr be ? ToString(rprim).
            const rstr = try rprim.toString(agent);

            // iii. Return the string-concatenation of lstr and rstr.
            return Value.from(
                try std.mem.concat(agent.gc_allocator, u8, &[_][]const u8{ lstr, rstr }),
            );
        }

        // d. Set lval to lprim.
        final_lval = lprim;

        // e. Set rval to rprim.
        final_rval = rprim;
    }

    // 2. NOTE: At this point, it must be a numeric operation.

    // 3. Let lnum be ? ToNumeric(lval).
    const lnum = try final_lval.toNumeric(agent);

    // 4. Let rnum be ? ToNumeric(rval).
    const rnum = try final_rval.toNumeric(agent);

    // 5. If Type(lnum) is not Type(rnum), throw a TypeError exception.
    if (@intFromEnum(lnum) != @intFromEnum(rnum)) {
        return agent.throwException(
            .type_error,
            "Left-hand side and right-hand side of numeric binary expression must have the same type",
        );
    }

    // 6. If lnum is a BigInt, then
    if (lnum == .big_int) switch (operator) {
        // a. If opText is **, return ? BigInt::exponentiate(lnum, rnum).
        .@"**" => return Value.from(try lnum.big_int.exponentiate(agent, rnum.big_int)),

        // b. If opText is /, return ? BigInt::divide(lnum, rnum).
        .@"/" => return Value.from(try lnum.big_int.divide(agent, rnum.big_int)),

        // c. If opText is %, return ? BigInt::remainder(lnum, rnum).
        .@"%" => return Value.from(try lnum.big_int.remainder(agent, rnum.big_int)),

        // d. If opText is >>>, return ? BigInt::unsignedRightShift(lnum, rnum).
        .@">>>" => return Value.from(try lnum.big_int.unsignedRightShift(agent, rnum.big_int)),

        else => {},
    };

    // 7. Let operation be the abstract operation associated with opText and Type(lnum) in the following table:
    // 8. Return operation(lnum, rnum).
    switch (operator) {
        .@"**" => return Value.from(lnum.number.exponentiate(rnum.number)),
        .@"*" => switch (lnum) {
            .number => return Value.from(lnum.number.multiply(rnum.number)),
            .big_int => return Value.from(try lnum.big_int.multiply(agent, rnum.big_int)),
        },
        .@"/" => return Value.from(lnum.number.divide(rnum.number)),
        .@"%" => return Value.from(lnum.number.remainder(rnum.number)),
        .@"+" => switch (lnum) {
            .number => return Value.from(lnum.number.add(rnum.number)),
            .big_int => return Value.from(try lnum.big_int.add(agent, rnum.big_int)),
        },
        .@"-" => switch (lnum) {
            .number => return Value.from(lnum.number.subtract(rnum.number)),
            .big_int => return Value.from(try lnum.big_int.subtract(agent, rnum.big_int)),
        },
        .@"<<" => switch (lnum) {
            .number => return Value.from(lnum.number.leftShift(rnum.number)),
            .big_int => return Value.from(try lnum.big_int.leftShift(agent, rnum.big_int)),
        },
        .@">>" => switch (lnum) {
            .number => return Value.from(lnum.number.signedRightShift(rnum.number)),
            .big_int => return Value.from(try lnum.big_int.signedRightShift(agent, rnum.big_int)),
        },
        .@">>>" => return Value.from(lnum.number.unsignedRightShift(rnum.number)),
        .@"&" => switch (lnum) {
            .number => return Value.from(lnum.number.bitwiseAND(rnum.number)),
            .big_int => return Value.from(try lnum.big_int.bitwiseAND(agent, rnum.big_int)),
        },
        .@"^" => switch (lnum) {
            .number => return Value.from(lnum.number.bitwiseXOR(rnum.number)),
            .big_int => return Value.from(try lnum.big_int.bitwiseXOR(agent, rnum.big_int)),
        },
        .@"|" => switch (lnum) {
            .number => return Value.from(lnum.number.bitwiseOR(rnum.number)),
            .big_int => return Value.from(try lnum.big_int.bitwiseOR(agent, rnum.big_int)),
        },
    }
}

/// 15.2.5 Runtime Semantics: InstantiateOrdinaryFunctionExpression
/// https://tc39.es/ecma262/#sec-runtime-semantics-instantiateordinaryfunctionexpression
fn instantiateOrdinaryFunctionExpression(
    agent: *Agent,
    function_expression: ast.FunctionExpression,
    default_name: ?[]const u8,
) !Object {
    const realm = agent.currentRealm();

    // FunctionExpression : function BindingIdentifier ( FormalParameters ) { FunctionBody }
    if (function_expression.identifier) |identifier| {
        // 1. Assert: name is not present.
        std.debug.assert(default_name == null);

        // 2. Set name to StringValue of BindingIdentifier.
        const name = identifier;

        // 3. Let outerEnv be the running execution context's LexicalEnvironment.
        const outer_env = agent.runningExecutionContext().ecmascript_code.?.lexical_environment;

        // 4. Let funcEnv be NewDeclarativeEnvironment(outerEnv).
        const func_env = try newDeclarativeEnvironment(agent.gc_allocator, outer_env);

        // TODO: 5. Perform ! funcEnv.CreateImmutableBinding(name, false).

        // 6. Let privateEnv be the running execution context's PrivateEnvironment.
        const private_env = agent.runningExecutionContext().ecmascript_code.?.private_environment;

        // 7. Let sourceText be the source text matched by FunctionExpression.
        const source_text = function_expression.source_text;

        // 8. Let closure be OrdinaryFunctionCreate(%Function.prototype%, sourceText,
        //    FormalParameters, FunctionBody, non-lexical-this, funcEnv, privateEnv).
        const closure = try ordinaryFunctionCreate(
            agent,
            try realm.intrinsics.@"%Function.prototype%"(),
            source_text,
            function_expression.formal_parameters,
            function_expression.function_body,
            .non_lexical_this,
            .{ .declarative_environment = func_env },
            private_env,
        );

        // 9. Perform SetFunctionName(closure, name).
        try setFunctionName(closure, PropertyKey.from(name), null);

        // 10. Perform MakeConstructor(closure).
        try makeConstructor(closure, .{});

        // TODO: 11. Perform ! funcEnv.InitializeBinding(name, closure).

        // 12. Return closure.
        return closure;
    }
    // FunctionExpression : function ( FormalParameters ) { FunctionBody }
    else {
        // 1. If name is not present, set name to "".
        const name = default_name orelse "";

        // 2. Let env be the LexicalEnvironment of the running execution context.
        const env = agent.runningExecutionContext().ecmascript_code.?.lexical_environment;

        // 3. Let privateEnv be the running execution context's PrivateEnvironment.
        const private_env = agent.runningExecutionContext().ecmascript_code.?.private_environment;

        // 4. Let sourceText be the source text matched by FunctionExpression.
        const source_text = function_expression.source_text;

        // 5. Let closure be OrdinaryFunctionCreate(%Function.prototype%, sourceText,
        //    FormalParameters, FunctionBody, non-lexical-this, env, privateEnv).
        const closure = try ordinaryFunctionCreate(
            agent,
            try realm.intrinsics.@"%Function.prototype%"(),
            source_text,
            function_expression.formal_parameters,
            function_expression.function_body,
            .non_lexical_this,
            env,
            private_env,
        );

        // 6. Perform SetFunctionName(closure, name).
        try setFunctionName(closure, PropertyKey.from(name), null);

        // 7. Perform MakeConstructor(closure).
        try makeConstructor(closure, .{});

        // 8. Return closure.
        return closure;
    }
}

pub fn executeInstruction(self: *Self, executable: Executable, instruction: Instruction) !void {
    switch (instruction) {
        .apply_string_or_numeric_binary_operator => {
            const operator_type = self.fetchIndex(executable);
            const operator: ast.BinaryExpression.Operator = @enumFromInt(operator_type);
            const rval = self.stack.pop();
            const lval = self.stack.pop();
            self.result = try applyStringOrNumericBinaryOperator(self.agent, lval, operator, rval);
        },
        .array_create => self.result = Value.from(try arrayCreate(self.agent, 0, null)),
        .array_set_length => {
            const length = self.fetchIndex(executable);
            const array = self.result.?.object;
            // From ArrayAccumulation:
            // 2. Perform ? Set(array, "length", ð”½(len), true).
            try array.set(PropertyKey.from("length"), Value.from(length), .throw);
        },
        .array_set_value => {
            const index = self.fetchIndex(executable);
            const init_value = self.stack.pop();
            const array = self.stack.pop().object;
            // From ArrayAccumulation:
            // 4. Perform ! CreateDataPropertyOrThrow(array, ! ToString(ð”½(nextIndex)), initValue).
            array.createDataPropertyOrThrow(
                PropertyKey.from(@as(PropertyKey.IntegerIndex, index)),
                init_value,
            ) catch |err| try noexcept(err);
            self.result = Value.from(array);
        },
        .bitwise_not => {
            const value = self.result.?;
            self.result = switch (value) {
                .number => |number| Value.from(number.bitwiseNOT()),
                .big_int => |big_int| Value.from(try big_int.bitwiseNOT()),
                else => unreachable,
            };
        },
        .evaluate_call => {
            const maybe_reference = self.reference_stack.getLast();
            const argument_count = self.fetchIndex(executable);
            const strict = self.fetchIndex(executable) == 1;
            var arguments = try std.ArrayList(Value).initCapacity(
                self.agent.gc_allocator,
                argument_count,
            );
            for (0..argument_count) |_| {
                const argument = self.stack.pop();
                try arguments.append(argument);
            }
            std.mem.reverse(Value, arguments.items);
            const this_value = self.stack.pop();
            const function = self.stack.pop();

            const realm = self.agent.currentRealm();
            const eval = try realm.intrinsics.@"%eval%"();

            // 6. If ref is a Reference Record, IsPropertyReference(ref) is false, and
            //    ref.[[ReferencedName]] is "eval", then
            if (maybe_reference) |reference| {
                if (!reference.isPropertyReference() and
                    reference.referenced_name == .string and
                    std.mem.eql(u8, reference.referenced_name.string, "eval") and

                    // a. If SameValue(func, %eval%) is true, then
                    function.object.ptr == eval.ptr)
                {
                    self.result = try directEval(self.agent, arguments.items, strict);
                    return;
                }
            }

            self.result = try evaluateCall(
                self.agent,
                function,
                this_value,
                arguments.items,
            );
        },
        .evaluate_new => {
            const argument_count = self.fetchIndex(executable);
            var arguments = try std.ArrayList(Value).initCapacity(
                self.agent.gc_allocator,
                argument_count,
            );
            defer arguments.deinit();
            for (0..argument_count) |_| {
                const argument = self.stack.pop();
                try arguments.append(argument);
            }
            std.mem.reverse(Value, arguments.items);
            const constructor = self.stack.pop();
            self.result = try evaluateNew(self.agent, constructor, arguments.items);
        },
        // 13.3.3 EvaluatePropertyAccessWithExpressionKey ( baseValue, expression, strict )
        // https://tc39.es/ecma262/#sec-evaluate-property-access-with-expression-key
        .evaluate_property_access_with_expression_key => {
            // 1. Let propertyNameReference be ? Evaluation of expression.
            // 2. Let propertyNameValue be ? GetValue(propertyNameReference).
            const property_name_value = self.stack.pop();

            const strict = self.fetchIndex(executable) == 1;
            const base_value = self.stack.pop();

            // 3. Let propertyKey be ? ToPropertyKey(propertyNameValue).
            const property_key = try property_name_value.toPropertyKey(self.agent);

            // 4. Return the Reference Record {
            //      [[Base]]: baseValue,
            //      [[ReferencedName]]: propertyKey,
            //      [[Strict]]: strict,
            //      [[ThisValue]]: empty
            //    }.
            self.reference = Reference{
                .base = .{ .value = base_value },
                .referenced_name = switch (property_key) {
                    .string => |string| .{ .string = string },
                    .symbol => |symbol| .{ .symbol = symbol },
                    .integer_index => |integer_index| .{
                        .string = try std.fmt.allocPrint(
                            self.agent.gc_allocator,
                            "{}",
                            .{integer_index},
                        ),
                    },
                },
                .strict = strict,
                .this_value = null,
            };
        },
        // 13.3.4 EvaluatePropertyAccessWithIdentifierKey ( baseValue, identifierName, strict )
        // https://tc39.es/ecma262/#sec-evaluate-property-access-with-identifier-key
        .evaluate_property_access_with_identifier_key => {
            // 1. Let propertyNameString be StringValue of identifierName.
            const property_name_string = self.fetchIdentifier(executable);

            const strict = self.fetchIndex(executable) == 1;
            const base_value = self.stack.pop();

            // 2. Return the Reference Record {
            //      [[Base]]: baseValue,
            //      [[ReferencedName]]: propertyNameString,
            //      [[Strict]]: strict,
            //      [[ThisValue]]: empty
            //    }.
            self.reference = Reference{
                .base = .{ .value = base_value },
                .referenced_name = .{ .string = property_name_string },
                .strict = strict,
                .this_value = null,
            };
        },
        .get_value => {
            if (self.reference) |reference| self.result = try reference.getValue(self.agent);
            self.reference = null;
        },
        .greater_than => {
            const rval = self.stack.pop();
            const lval = self.stack.pop();

            // 5. Let r be ? IsLessThan(rval, lval, false).
            const result = try isLessThan(self.agent, rval, lval, .right_first);

            // 6. If r is undefined, return false. Otherwise, return r.
            self.result = Value.from(result orelse false);
        },
        .greater_than_equals => {
            const rval = self.stack.pop();
            const lval = self.stack.pop();

            // 5. Let r be ? IsLessThan(lval, rval, true).
            const result = try isLessThan(self.agent, lval, rval, .left_first);

            // 6. If r is either true or undefined, return false. Otherwise, return true.
            self.result = Value.from(!(result orelse true));
        },
        .has_property => {
            const rval = self.stack.pop();
            const lval = self.stack.pop();

            // 5. If rval is not an Object, throw a TypeError exception.
            if (rval != .object) return self.agent.throwException(
                .type_error,
                "Right-hand side of 'in' operator must be an object",
            );

            // 6. Return ? HasProperty(rval, ? ToPropertyKey(lval)).
            self.result = Value.from(
                try rval.object.hasProperty(try lval.toPropertyKey(self.agent)),
            );
        },
        .instanceof_operator => {
            const rval = self.stack.pop();
            const lval = self.stack.pop();

            // 5. Return ? InstanceofOperator(lval, rval).
            self.result = Value.from(try instanceofOperator(self.agent, lval, rval));
        },
        .instantiate_ordinary_function_expression => {
            const function_expression = self.fetchFunctionExpression(executable);
            const closure = try instantiateOrdinaryFunctionExpression(
                self.agent,
                function_expression,
                null,
            );
            self.result = Value.from(closure);
        },
        .is_loosely_equal => {
            const rval = self.stack.pop();
            const lval = self.stack.pop();

            // 5. Return IsLooselyEqual(rval, lval).
            self.result = Value.from(try isLooselyEqual(self.agent, rval, lval));
        },
        .is_strictly_equal => {
            const rval = self.stack.pop();
            const lval = self.stack.pop();

            // 5. Return IsStrictlyEqual(rval, lval).
            self.result = Value.from(isStrictlyEqual(rval, lval));
        },
        .jump => self.ip = self.fetchIndex(executable),
        .jump_conditional => {
            const ip_consequent = self.fetchIndex(executable);
            const ip_alternate = self.fetchIndex(executable);
            const value = self.result.?;
            self.ip = if (value.toBoolean()) ip_consequent else ip_alternate;
        },
        .less_than => {
            const rval = self.stack.pop();
            const lval = self.stack.pop();

            // 5. Let r be ? IsLessThan(lval, rval, true).
            const result = try isLessThan(self.agent, lval, rval, .left_first);

            // 6. If r is undefined, return false. Otherwise, return r.
            self.result = Value.from(result orelse false);
        },
        .less_than_equals => {
            const rval = self.stack.pop();
            const lval = self.stack.pop();

            // 5. Let r be ? IsLessThan(rval, lval, false).
            const result = try isLessThan(self.agent, rval, lval, .right_first);

            // 6. If r is either true or undefined, return false. Otherwise, return true.
            self.result = Value.from(!(result orelse true));
        },
        .load => {
            const value = self.result.?;
            try self.stack.append(value);
        },
        .load_constant => {
            const value = self.fetchConstant(executable);
            try self.stack.append(value);
        },
        .load_this_value => {
            const maybe_reference = self.reference_stack.getLast();
            const this_value = evaluateCallGetThisValue(maybe_reference);
            try self.stack.append(this_value);
        },
        .logical_not => {
            const value = self.result.?;
            self.result = Value.from(!value.toBoolean());
        },
        .object_create => {
            const object = try ordinaryObjectCreate(
                self.agent,
                try self.agent.currentRealm().intrinsics.@"%Object.prototype%"(),
            );
            self.result = Value.from(object);
        },
        .object_set_property => {
            const property_value = self.stack.pop();
            const property_name = try self.stack.pop().toPropertyKey(self.agent);
            const object = self.stack.pop().object;
            // From PropertyDefinitionEvaluation:
            // 5. Perform ! CreateDataPropertyOrThrow(object, propName, propValue).
            object.createDataPropertyOrThrow(property_name, property_value) catch |err| try noexcept(err);
            self.result = Value.from(object);
        },
        .pop_exception_jump_target => _ = self.exception_jump_target_stack.pop(),
        .pop_reference => _ = self.reference_stack.pop(),
        .push_exception_jump_target => {
            const jump_target = self.fetchIndex(executable);
            try self.exception_jump_target_stack.append(jump_target);
        },
        .push_reference => try self.reference_stack.append(self.reference),
        .put_value => {
            const lref = self.reference_stack.getLast().?;
            const rval = self.result.?;
            try lref.putValue(self.agent, rval);
        },
        .resolve_binding => {
            const name = self.fetchIdentifier(executable);
            const strict = self.fetchIndex(executable) == 1;
            self.reference = try self.agent.resolveBinding(name, null, strict);
        },
        .resolve_this_binding => self.result = try self.agent.resolveThisBinding(),
        .rethrow_exception_if_any => if (self.exception) |value| {
            self.agent.exception = value;
            return error.ExceptionThrown;
        },
        .@"return" => {}, // Handled in run()
        .store => self.result = self.stack.pop(),
        .store_constant => {
            const value = self.fetchConstant(executable);
            self.result = value;
        },
        .throw => {
            const value = self.result.?;
            self.agent.exception = value;
            return error.ExceptionThrown;
        },
        .to_number => {
            const value = self.result.?;
            self.result = Value.from(try value.toNumber(self.agent));
        },
        .to_numeric => {
            const value = self.result.?;
            const numeric = try value.toNumeric(self.agent);
            self.result = switch (numeric) {
                .number => |number| Value.from(number),
                .big_int => |big_int| Value.from(big_int),
            };
        },
        .typeof => {
            // 1. Let val be ? Evaluation of UnaryExpression.
            // NOTE: This is part of the generated bytecode.

            // 2. If val is a Reference Record, then
            if (self.reference) |reference| {
                // a. If IsUnresolvableReference(val) is true, return "undefined".
                if (reference.isUnresolvableReference()) {
                    self.result = Value.from("undefined");
                    return;
                }
            }

            // 3. Set val to ? GetValue(val).
            const value = if (self.reference) |reference|
                try reference.getValue(self.agent)
            else
                self.result.?;

            self.result = switch (value) {
                // 4. If val is undefined, return "undefined".
                .undefined => Value.from("undefined"),

                // 5. If val is null, return "object".
                .null => Value.from("object"),

                // 6. If val is a String, return "string".
                .string => Value.from("string"),

                // 7. If val is a Symbol, return "symbol".
                .symbol => Value.from("symbol"),

                // 8. If val is a Boolean, return "boolean".
                .boolean => Value.from("boolean"),

                // 9. If val is a Number, return "number".
                .number => Value.from("number"),

                // 10. If val is a BigInt, return "bigint".
                .big_int => Value.from("bigint"),

                // 11. Assert: val is an Object.
                .object => |object| blk: {
                    // 12. NOTE: This step is replaced in section B.3.6.3.

                    // 13. If val has a [[Call]] internal slot, return "function".
                    if (object.internalMethods().call) |_| break :blk Value.from("function");

                    // 14. Return "object".
                    break :blk Value.from("object");
                },
            };
        },
        .unary_minus => {
            const value = self.result.?;
            self.result = switch (value) {
                .number => |number| Value.from(number.unaryMinus()),
                .big_int => |big_int| Value.from(try big_int.unaryMinus()),
                else => unreachable,
            };
        },
        _ => unreachable,
    }
}

pub fn run(self: *Self, executable: Executable) !Completion {
    while (self.fetchInstruction(executable)) |instruction| {
        self.executeInstruction(executable, instruction) catch |err| {
            if (self.exception_jump_target_stack.items.len != 0) {
                // TODO: Store exceptiom and create catch binding
                self.exception = self.agent.exception;
                self.agent.exception = null;
                self.ip = self.exception_jump_target_stack.getLast();
            } else return err;
        };
        if (instruction == .@"return") {
            return .{ .type = .@"return", .value = self.result, .target = null };
        }
    }
    return Completion.normal(self.result);
}
